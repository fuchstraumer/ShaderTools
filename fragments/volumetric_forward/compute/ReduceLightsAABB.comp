#include "Structures.glsl"

#pragma USE_RESOURCES VOLUMETRIC_FORWARD_LIGHTS
#pragma USE_RESOURCES SORT_RESOURCES

$SPC const uint NumThreads = 512;
$SPC const uint ReductionType = 0;

layout(local_size_x = 512, local_size_y = 1, local_size_z = 1) in;
shared vec4 gs_AABBMin[NumThreads];
shared vec4 gs_AABBMax[NumThreads];

const float FLT_MAX = 3.402823466e+36f;

void LogStepReduction() {
    uint reduceIndex = uint(NumThreads) >> 1;
    while (reduceIndex > 32) {
        if (gl_LocalInvocationIndex < reduceIndex) {
            gs_AABBMin[gl_LocalInvocationIndex] = min(gs_AABBMin[gl_LocalInvocationIndex], gs_AABBMin[gl_LocalInvocationIndex + reduceIndex]);
            gs_AABBMax[gl_LocalInvocationIndex] = max(gs_AABBMax[gl_LocalInvocationIndex], gs_AABBMax[gl_LocalInvocationIndex + reduceIndex]);
        }

        groupMemoryBarrier();

        reduceIndex >>= 1;
    }

    if (gl_LocalInvocationIndex < 32) {
        while (reduceIndex > 0) {
            if (NumThreads >= reduceIndex << 1) {
                gs_AABBMin[gl_LocalInvocationIndex] = min(gs_AABBMin[gl_LocalInvocationIndex], gs_AABBMin[gl_LocalInvocationIndex + reduceIndex]);
                gs_AABBMax[gl_LocalInvocationIndex] = max(gs_AABBMax[gl_LocalInvocationIndex], gs_AABBMax[gl_LocalInvocationIndex + reduceIndex]);
            }

            reduceIndex >>= 1;
        }

        if (gl_LocalInvocationIndex == 0) {
            LightAABBs.Data[gl_WorkGroupID.x].Min = gs_AABBMin[gl_LocalInvocationIndex];
            LightAABBs.Data[gl_WorkGroupID.x].Max = gs_AABBMax[gl_LocalInvocationIndex];
        }
    }
}

void reduce0() {
    vec4 aabb_min = vec4( FLT_MAX, FLT_MAX, FLT_MAX, 1.0f);
    vec4 aabb_max = vec4(-FLT_MAX,-FLT_MAX,-FLT_MAX, 1.0f);

    for (uint i = uint(gl_GlobalInvocationID.x); i < LightCounts.NumPointLights; i += uint(NumThreads) * DispatchParams.NumThreadGroups.x) {
        aabb_min = min(aabb_min, PointLights.Data[i].PositionViewSpace - PointLights.Data[i].Range);
        aabb_max = max(aabb_max, PointLights.Data[i].PositionViewSpace + PointLights.Data[i].Range);
    }

    for (uint i = uint(gl_GlobalInvocationID.x); i < LightCounts.NumSpotLights; i += uint(NumThreads) * DispatchParams.NumThreadGroups.x) {
        aabb_min = min(aabb_min, SpotLights.Data[i].PositionViewSpace - SpotLights.Data[i].Range);
        aabb_max = min(aabb_max, SpotLights.Data[i].PositionViewSpace + SpotLights.Data[i].Range);
    }

    gs_AABBMin[gl_LocalInvocationIndex] = aabb_min;
    gs_AABBMax[gl_LocalInvocationIndex] = aabb_max;

    groupMemoryBarrier();

    LogStepReduction();
}

void reduce1() {
    vec4 aabb_min = vec4( FLT_MAX, FLT_MAX, FLT_MAX, 1.0f);
    vec4 aabb_max = vec4(-FLT_MAX,-FLT_MAX,-FLT_MAX, 1.0f);

    for (uint i = gl_LocalInvocationIndex; i < ReductionParams.NumElements; i += uint(NumThreads) * DispatchParams.NumThreadGroups.x) {
        aabb_min = min(aabb_min, LightAABBs.Data[i].Min);
        aabb_max = max(aabb_max, LightAABBs.Data[i].Max);
    }

    gs_AABBMin[gl_LocalInvocationIndex] = aabb_min;
    gs_AABBMax[gl_LocalInvocationIndex] = aabb_max;

    groupMemoryBarrier();

    LogStepReduction();
}

void main() {
    switch (ReductionType) {
    case 0:
        reduce0();
        break;
    case 1:
        reduce1();
        break;
    };
}
